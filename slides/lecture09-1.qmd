---
title: "Economic Dispatch"
subtitle: "Lecture 18"
author: "Vivek Srikrishnan"
course: "BEE 4750"
institution: "Cornell University"
date: "October 16, 2023"
format:
    revealjs:
        slide-number: c/t
        show-slide-number: all
        center-title-slide: true
        width: 1280
        height: 720
        transition: none
        toc: true
        toc-depth: 1
        toc-title: "Overview"
        history: false
        link-external-newwindow: true
        theme: ../sass/slides.scss
        footer: "[BEE 4750, Environmental Systems Analysis](https://viveks.me/environmental-systems-analysis)"
        template-partials:
            - title-slide.html
        menu:
            numbers: true
        html-math-method: mathjax
        include-in-header: mathjax-config.html
        date-format: long
        highlight-style: tango
        code-line-numbers: false

execute:
    freeze: auto
---

```{julia}
#| echo: false
#| output: false

import Pkg
Pkg.activate(".")
Pkg.instantiate()
```

```{julia}
#| echo: false
#| output: false

using JuMP
using HiGHS
using Plots
using StatsPlots
using Measures
using LaTeXStrings
using DataFrames
using CSV
using MarkdownTables
```

# Review and Questions

## Last Class

- Overview of power systems decision problems
- Many are LPs (at least classically...)
- Generating capacity expansion

## Questions?

{{< include _poll-prompt.qmd >}}

# Electric Power System Decision Problems

## Overview of Electric Power Systems

![Power Systems Schematic](https://upload.wikimedia.org/wikipedia/commons/4/41/Electricity_grid_simple-_North_America.svg)

::: {.caption}
Source: [Wikipedia](https://en.wikipedia.org/wiki/Electric_power_transmission)
:::

## Decisions Problems for Power Systems

![Decision Problems for Power Systems by Time Scale](figures/elec-decision-problems.svg)

::: {.caption}
Adapted from Perez-Arriaga, Ignacio J., Hugh Rudnick, and Michel Rivier (2009)
:::

# Single-Period Economic Dispatch

## Economic Dispatch

**Decision Problem**: Given a fleet of (online) generators, how do we meet demand at lowest cost?

::: {.fragment .fade-in}
**New Constraints**: Power plants are generally subject to engineering constraints that we had previously neglected:

- Ramping limits
- Minimum/Maximum power outputs
- May include network constraints (we will ignore here)
:::


## Single-Period Economic Dispatch

What are our variables?

::: {.fragment .fade-in}
| Variable | Meaning |
|:-----:|:---------------| 
| $d$ | demand (MW) |
| $y_g$ | generation (MW) by generator $g \in \mathcal{G}$ | 
| $VarCost_g$ | variable generation cost (\$/MWh) for generator $g$ |
| $P^{\text{min/max}}_g$ | generation limits (MW) for generator $g$ | 
:::

## Note on Variable Costs

In practice, variable costs come from:

- labor costs;
- equipment upkeep;
- **fuel costs** (the big one!).

Fuel is the big variable. The translation of fuel costs to generation costs depends on the efficiency (the **heat rate**) of the plant.

##  Note on Variable Costs
:::: {.columns}
::: {.column width=60%}

These costs are often actually *quadratic*, not linear, due to efficiency changes.

But we can assume a piecewise linear approximation.

:::
::: {.column width=40%}
![Typical cost curve for a thermal generator](figures/thermal-cost-curve.jpeg)

::: {.caption}
Source: [Ross Baldrick, UT Austin](https://users.ece.utexas.edu/~baldick/classes/394V/Dispatch.pdf)
:::
:::
::::

## Single-Period Economic Dispatch

Then the economic dispatch problem becomes: 

$$
\begin{align}
\min_{y_g} & \sum_g VarCost_g \times y_g & \\
\text{subject to:} \quad & \sum_g y_g \geq d & \forall g \in \mathcal{G} \\[0.5em]
& y_g \leq P^{\text{max}}_g & \forall g \in \mathcal{G} \\[0.5em]
& y_g \geq P^{\text{min}}_g & \forall g \in \mathcal{G}
\end{align}
$$


## Single-Period Example: Data

```{julia}
#| output: false
gens = DataFrame(CSV.File("data/economic_dispatch/generators.csv"))
```

- 1 biomass, 50 MW capacity, \$5/MWh
- 1 hydroelectric, 500 MW capacity, \$0/MWh
- 5 natural gas CCGT, 25-220 MW minimum, 50-620 MW capacity \$22-37/MWh
- 6 natural gas CT, 0-73 MW minimum, 48-100 MW capacity, \$38-45/MWh

Let's assume demand is 2400 MW.

## Single-Period Results

```{julia}

# define sets
G = 1:nrow(gens)
NSECost = 9000
d = 2400

single_ed = Model(HiGHS.Optimizer)
@variable(single_ed, gens[g, :Pmin] <= y[g in G] <= gens[g, :Pmax])
@objective(single_ed, Min, sum(gens[!, :VarCost] .* y))
@constraint(single_ed, demand, sum(y) >= d)
set_silent(single_ed)
optimize!(single_ed)

leg_gps = repeat(["Dispatched Generation", "Minimum Generation"], inner = nrow(gens))
groupedbar(repeat(gens[!, :Plant], outer=2), Matrix(hcat(Vector(value.(y)), gens[!, :Pmin])), group=leg_gps, tickfontsize=14, guidefontsize=14, legendfontsize=14, xrotation=45, bottom_margin=20mm, left_margin=10mm, ylabel="Generation (MW)")
plot!(size=(1300, 400))
```

The cost of operating the system is \$51,016, and the shadow price of the demand constraint is -\$36.

## Cost of Marginal Generation

How can we understand these results?

::: {.fragment .fade-in}
```{julia}
gens.cap = gens.Pmax .- gens.Pmin
sort!(gens, [:VarCost, order(:cap, rev=true)])

function plot_supply_curve(supply_curve, demand) 
    rectangle(w, h, x, y) = Shape(x .+ [0,w,w,0], y .+ [0,0,h,h]) 

    p = plot(grid=:false, color_palette=:mk_15, size=(700, 400), left_margin=10mm, bottom_margin=10mm, legendfontsize=15, tickfontsize=14, guidefontsize=16) 
    plot!(legend=:outerright, legendcolumns=1)
    marg_gen = 0 
    marg_price = 0 
    x = 0 

    plot!(p, rectangle(sum(supply_curve.Pmin), 0.5, x, 0), opacity=.5, label = "minimum", color="black") 
    x = x + sum(supply_curve.Pmin) 
    for i in 1:nrow(supply_curve) 
        if supply_curve[i, :VarCost] == 0 
            plot!(p, rectangle(supply_curve[i, :cap], 0.5,x,0), opacity=.5, label = supply_curve[i, :Plant]) 
        else 
            plot!(p, rectangle(supply_curve[i, :cap], supply_curve[i, :VarCost],x,0), opacity=.5, label = supply_curve[i, :Plant]) 
        end 
        if (x < demand) && (x + supply_curve[i,:cap] > demand) 
            marg_gen = i 
            marg_price = supply_curve[i,:VarCost] 
        end 
        x = x + supply_curve[i,:cap] 
    end 
    vline!([demand],linecolor="black",linewidth=3, linestyle=:dash, label = "demand") 
    hline!([marg_price],linecolor="blue", linestyle=:dot, linewidth=3, label = "Electricity Price") 

    title!("Dispatch Stack Supply Curve") 
    xlabel!("Capacity (MW)") 
    ylabel!("Marginal Cost (\$\$/MW)") 

    return p 
end 
d = 2400
p = plot_supply_curve(gens, d)
plot!(p, size=(1300, 450))

```
:::

## Dispatch Stack and Merit Order

This supply curve (the **dispatch stack**) gives the **merit order**.

```{julia}
p
```

## Dispatch Stack and Merit Order

***What might complicate this simple merit ordering based on variable costs?***



# Multiple-Period Dispatch

## Ramping Constraints

Now, let's consider multiple time periods. 

Not only do we need to meet demand at every time period, but we have additional **ramping** constraints.

Plants can only increase and decrease their output by so much from time to time, by $R_g$.

## Multi-Period Formulation

$$
\begin{align}
\min_{y_{g,t}} & \sum_g VarCost_g \times \sum_t y_{g,t} & \\
\text{subject to:} \quad & \sum_g y_{g,t} = d_t & \\[0.5em]
& y_{g,t} \leq P^{\text{max}}_g  & \forall t \in \mathcal{T}, g \in \mathcal{G} \\[0.5em]
& y_{g,t} \geq P^{\text{min}}_g & \forall t \in \mathcal{T}, g \in \mathcal{G} \\[0.5em]
& \color{red}y_{g,t+1} - y_{g, t} \leq R_g & \forall t \in \mathcal{T}, g \in \mathcal{G} \\[0.5em]
& \color{red}y_{g,t} - y_{g, t+1} \leq R_g &  \forall t \in \mathcal{T}, g \in \mathcal{G}
\end{align}
$$

## Multi-Period Generator Data

Ramping constraints can vary strongly by generator type, which, combined with costs, influences whether we view generators as base load or peaking.

- Nuclear plants generally have a very narrow range in which they can operate;
- Combustion turbine gas plants can ramp from 0-100% very rapidly.

## Multi-Period Generator Data

We'll make this simple for this problem:

- Biomass, hydroelectric, CT can ramp from 0-100% each hour.
- CCGT plants can ramp from 50-100% of maximum capacity.

## Demand Curve

```{julia}
#| label: fig-demand
#| fig-cap: Demand for 2020 in NYISO Zone C
#| fig-align: center

NY_demand = DataFrame(CSV.File("data/capacity_expansion/2020_hourly_load_NY.csv"))
rename!(NY_demand, :"Time Stamp" => :Date)
d = NY_demand[:, [:Date, :C]]
rename!(d, :C => :Demand)
n = 289 # pick day
T_period = (n*24+1):((n+1)*24)
d = d[T_period, :]
@df d plot(:Date, :Demand, xlabel="Date", ylabel="Demand (MWh)", label=:false, tickfontsize=14, guidefontsize=14, xrot=45, bottommargin=20mm, leftmargin=7mm)
plot!(size=(1200, 550))
```

## Multi-Period Results

```{julia}
G = 1:nrow(gens)
T = 1:nrow(d)

multi_ed = Model(HiGHS.Optimizer)
@variable(multi_ed, gens[g, :Pmin] <= y[g in G, t in T] <= gens[g, :Pmax])
@objective(multi_ed, Min, sum(gens[:, :VarCost] .* [sum(y[g, :]) for g in G]))
@constraint(multi_ed, load[t in T], sum(y[:, t]) == d[t, :Demand])
@constraint(multi_ed, rampup[g in G, t in 1:length(T)-1], y[g, t+1] - y[g, t] <= gens[g, :Ramp])
@constraint(multi_ed, rampdown[g in G, t in 1:length(T)-1], y[g, t] - y[g, t+1] <= gens[g, :Ramp])
set_silent(multi_ed)
optimize!(multi_ed)

gen = value.(y).data
p = areaplot(gen', 
    label=permutedims(gens[:, :Plant]), 
    xlabel = "Hour", 
    ylabel ="Generated Electricity (MW)", 
    color_palette=:mk_15, 
    left_margin=5mm, 
    bottom_margin=5mm,
    grid=:false,
    ylim=(0, 1900),
    legendfontsize=16, tickfontsize=13, 
    guidefontsize=16
)
plot!(legend=:outerright, legendcolumns=1)
plot!(p, size=(1200, 550))
```

## The "Duck Curve"

:::: {.columns}
::: {.column width=50%}
Ramping and minimum generation play a major role in systems with high levels of renewable penetration.

For example, a prominent feature of grids with large solar generation is the "duck curve" (right).
:::
::: {.column width=50%}
![CAISO Duck Curve](figures/duck_curve.jpeg)

::: {.caption}
Source: [Power Magazine](https://www.powermag.com/duck-hunting-california-independent-system-operator/)
:::
:::
::::

# Key Takeaways

## Key Takeaways

- Capacity Expansion is a foundational power systems decision problem.
- Is an LP with some basic assumptions.
- We looked at a "greenfield" example: no existing plants.
- Decision problem becomes more complex with renewables (HW4) or "brownfield" (expanding existing fleet, possibly with retirements).

# Upcoming Schedule

## Next Classes

**Monday**: Economic Dispatch

**Wednesday/Friday**: Air Pollution

## Assessments

- Lab 3 due **tonight** at 9pm.
- HW4 assigned Monday (likely over the weekend).